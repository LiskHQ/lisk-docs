---
title: Meluncurkan Token Sendiri
slug: /building-on-lisk/add-token-to-lisk/custom-token
description: 'Pelajari cara memindahkan token ERC-20 Anda sendiri ke Lisk menggunakan bridge standar.'
keywords:
  [
    'ERC-20 contract',
    'Custom token',
    'Lisk Testnet',
    'Sepolia',
    'Ethereum',
    'Lisk Sepolia',
    'Lisk',
    'Lisk mainnet',
    'Optimism Superchain token list',
  ]
---

# Meluncurkan Token ERC-20 Anda Sendiri ke Lisk

{/* :::info
**Tutorial ini ditujukan untuk developer yang ingin memindahkan token ERC-20 baru mereka sendiri ke Lisk Mainnet menggunakan bridge.**
Jika Anda ingin memindahkan token yang sudah ada, Anda dapat mengikuti tutorial [Memindahkan Token ERC-20 dengan Optimism SDK](https://docs.optimism.io/builders/app-developers/tutorials/cross-dom-bridge-erc20).
::: */}

Dalam tutorial ini, Anda akan belajar cara memindahkan token ERC-20 kustom dari Ethereum atau Sepolia ke jaringan Lisk atau Lisk Sepolia.
Dengan menggunakan [Sistem Bridge Standar](https://docs.optimism.io/builders/dapp-developers/bridging/standard-bridge), tutorial ini ditujukan untuk developer yang sudah memiliki token ERC-20 di Ethereum dan ingin membuat representasi token tersebut di Lisk.

Pelajari langkah demi langkah cara membuat token kustom yang sesuai dengan interface [`IOptimismMintableERC20`](https://github.com/ethereum-optimism/optimism/blob/v1.1.4/packages/contracts-bedrock/src/universal/IOptimismMintableERC20.sol) sehingga dapat digunakan dengan Sistem Bridge Standar.
Token kustom memungkinkan Anda melakukan hal-hal seperti men-trigger logika tambahan setiap kali token dideposit.
Jika Anda tidak memerlukan fungsionalitas tambahan ini, pertimbangkan untuk mengikuti tutorial [Meluncurkan Token ERC-20 Standar ke Lisk](./standard-token).

## Prasyarat

:::note
Anda dapat meluncurkan **Token ERC-20 Anda Sendiri** di Lisk Mainnet dengan menggunakan proses yang sama.
Untuk meluncurkan ke mainnet, pastikan dompet Anda memiliki ETH yang cukup.

Teks berikut mencakup perintah untuk Lisk dan Lisk Sepolia demi kemudahan Anda.
Untuk informasi lebih lanjut, lihat [jaringan Lisk yang tersedia](/network-info) dan [cara menghubungkan dompet ke jaringan tersebut](/user/connecting-to-a-wallet).
:::

### Mendapatkan ETH di Sepolia dan Lisk Sepolia

Anda perlu mendapatkan ETH di kedua jaringan, Sepolia dan Lisk Sepolia.

:::info
Anda dapat menggunakan [ETH Sepolia Faucet](https://sepoliafaucet.com/) untuk mendapatkan ETH di Sepolia.
Anda dapat menggunakan [Superchain Faucet](https://app.optimism.io/faucet?utm_source=docs) untuk mendapatkan ETH di Lisk Sepolia.
:::

### Menambahkan Lisk Sepolia ke Dompet Anda

Tutorial ini menggunakan [Remix](https://remix.ethereum.org) untuk meluncurkan kontrak.
Anda perlu menambahkan jaringan Lisk atau Lisk Sepolia ke dompet Anda untuk mengikuti tutorial ini.
Silakan ikuti panduan [Cara Menghubungkan Lisk ke Dompet](/user/connecting-to-a-wallet) untuk menghubungkan dompet Anda ke jaringan Lisk atau Lisk Sepolia.

### Mendapatkan Alamat Token ERC-20 L1

Anda memerlukan token ERC-20 L1 untuk tutorial ini.
Jika Anda sudah memiliki token ERC-20 L1 yang diluncurkan di Ethereum Mainnet atau Sepolia, Anda dapat melewati langkah ini.
Untuk Sepolia, Anda dapat menggunakan token uji coba yang terletak di [`0x5589BB8228C07c4e15558875fAf2B859f678d129`](https://sepolia.etherscan.io/address/0x5589BB8228C07c4e15558875fAf2B859f678d129) yang menyertakan fungsi `faucet()` untuk me-mint token.

## Membuat Token ERC-20 L2

Setelah Anda memiliki token ERC-20 L1, Anda dapat membuat token ERC-20 L2 yang sesuai di jaringan Lisk atau Lisk Sepolia.
Tutorial ini menggunakan [Remix](https://remix.ethereum.org), sehingga Anda dapat dengan mudah meluncurkan token tanpa framework seperti [Hardhat](https://hardhat.org) atau [Foundry](https://getfoundry.sh).
Anda juga dapat mengikuti proses umum yang sama dalam framework pilihan Anda jika Anda mau.

Dalam bagian ini, Anda akan membuat token ERC-20 yang dapat di-deposit tetapi tidak dapat di-withdraw.
Ini hanyalah salah satu contoh dari banyak cara untuk menyesuaikan token L2 Anda.

### 1. Buka Remix

Akses [Remix](https://remix.ethereum.org) di browser Anda.

### 2. Buat file baru

Klik tombol ðŸ“„ ("Buat file baru") untuk membuat file Solidity kosong.
Anda dapat memberi nama file ini sesuai keinginan Anda, misalnya `custom-token.sol`.

### 3. Salin kontrak contoh

Salin kontrak contoh berikut ke dalam file baru Anda:

<details>
<summary>custom-token.sol</summary>
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import { ERC20 } from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import { IERC165 } from "@openzeppelin/contracts/utils/introspection/IERC165.sol";
import { IOptimismMintableERC20 } from "https://github.com/ethereum-optimism/optimism/blob/v1.1.4/packages/contracts-bedrock/src/universal/IOptimismMintableERC20.sol";

contract MyCustomL2Token is IOptimismMintableERC20, ERC20 {
    /// @notice Address of the corresponding version of this token on the remote chain.
    address public immutable REMOTE_TOKEN;

    /// @notice Address of the StandardBridge on this network.
    address public immutable BRIDGE;

    /// @notice Emitted whenever tokens are minted for an account.
    /// @param account Address of the account tokens are being minted for.
    /// @param amount  Amount of tokens minted.
    event Mint(address indexed account, uint256 amount);

    /// @notice Emitted whenever tokens are burned from an account.
    /// @param account Address of the account tokens are being burned from.
    /// @param amount  Amount of tokens burned.
    event Burn(address indexed account, uint256 amount);

    /// @notice A modifier that only allows the bridge to call.
    modifier onlyBridge() {
        require(msg.sender == BRIDGE, "MyCustomL2Token: only bridge can mint and burn");
        _;
    }

    /// @param _bridge      Address of the L2 standard bridge.
    /// @param _remoteToken Address of the corresponding L1 token.
    /// @param _name        ERC20 name.
    /// @param _symbol      ERC20 symbol.
    constructor(
        address _bridge,
        address _remoteToken,
        string memory _name,
        string memory _symbol
    )
        ERC20(_name, _symbol)
    {
        REMOTE_TOKEN = _remoteToken;
        BRIDGE = _bridge;
    }

    /// @custom:legacy
    /// @notice Legacy getter for REMOTE_TOKEN.
    function remoteToken() public view returns (address) {
        return REMOTE_TOKEN;
    }

    /// @custom:legacy
    /// @notice Legacy getter for BRIDGE.
    function bridge() public view returns (address) {
        return BRIDGE;
    }

    /// @notice ERC165 interface check function.
    /// @param _interfaceId Interface ID to check.
    /// @return Whether or not the interface is supported by this contract.
    function supportsInterface(bytes4 _interfaceId) external pure virtual returns (bool) {
        bytes4 iface1 = type(IERC165).interfaceId;
        // Interface corresponding to the updated OptimismMintableERC20 (this contract).
        bytes4 iface2 = type(IOptimismMintableERC20).interfaceId;
        return _interfaceId == iface1 || _interfaceId == iface2;
    }

    /// @notice Allows the StandardBridge on this network to mint tokens.
    /// @param _to     Address to mint tokens to.
    /// @param _amount Amount of tokens to mint.
    function mint(
        address _to,
        uint256 _amount
    )
        external
        virtual
        override(IOptimismMintableERC20)
        onlyBridge
    {
        _mint(_to, _amount);
        emit Mint(_to, _amount);
    }

    /// @notice Prevents tokens from being withdrawn to L1.
    function burn(
        address,
        uint256
    )
        external
        virtual
        override(IOptimismMintableERC20)
        onlyBridge
    {
        revert("MyCustomL2Token cannot be withdrawn");
    }
}
```
</details>

### 4. Tinjau kontrak contoh

Luangkan waktu untuk meninjau kontrak contoh.
Kontrak ini hampir sama dengan kontrak standar [`OptimismMintableERC20`](https://github.com/ethereum-optimism/optimism/blob/v1.1.4/packages/contracts-bedrock/src/universal/OptimismMintableERC20.sol), kecuali fungsi `_burn` telah dimodifikasi agar selalu gagal.

Kontrak untuk token kustom ini mewarisi dari interface [`IOptimismMintableERC20`](https://github.com/ethereum-optimism/optimism/blob/v1.1.4/packages/contracts-bedrock/src/universal/IOptimismMintableERC20.sol) dan kontrak `ERC20`.
Konstruktor menerima alamat dari L2 standard bridge, alamat token L1 yang sesuai, nama token ERC20, dan simbol token ERC20.
Fungsi `mint` memungkinkan bridge me-mint token untuk pengguna.
Karena bridge perlu mem-burn token ketika pengguna ingin me-withdraw-nya kembali ke L1, ini berarti pengguna tidak akan dapat me-withdraw token dari kontrak ini, sesuai dengan tujuan contoh ini.

```solidity
/// @notice Prevents tokens from being withdrawn to L1.
function burn(
    address,
    uint256
)
    external
    virtual
    override(IOptimismMintableERC20)
    onlyBridge
{
    revert("MyCustomL2Token cannot be withdrawn");
}
```

### 5. Mengompilasi Kontrak

Simpan file untuk secara otomatis mengompilasi kontrak.
Jika Anda telah menonaktifkan fitur kompilasi otomatis, Anda perlu mengompilasi kontrak secara manual dengan mengklik tab "Solidity Compiler" (ikon berbentuk huruf "S") dan menekan tombol biru "Compile".

### 6. Meluncurkan Kontrak

Buka tab deployment (ikon berbentuk logo Ethereum dengan panah mengarah ke kanan).
Pastikan bahwa environment Anda diatur ke "Injected Provider", dompet Anda terhubung ke jaringan Lisk atau Lisk Sepolia, dan Remix memiliki akses ke dompet Anda.
Kemudian, pilih kontrak `MyCustomL2Token` dari dropdown deployment dan luncurkan dengan parameter berikut:

```text
_BRIDGE:      "0x4200000000000000000000000000000000000007"
_REMOTETOKEN: "<L1 ERC-20 address>"
_NAME:        "My Custom Lisk L2 Token"
_SYMBOL:      "MCL2T"
```

:::tip
Jika Anda menggunakan token uji coba yang dijelaskan pada langkah [Mendapatkan Alamat Token ERC-20 L1](#mendapatkan-alamat-token-erc-20-l1), gunakan alamat `0x5589BB8228C07c4e15558875fAf2B859f678d129` untuk parameter `_REMOTETOKEN`.
:::

{/* ## Bridge Beberapa Token

Sekarang setelah Anda memiliki token ERC-20 L2, Anda dapat memindahkan beberapa token dari L1 ke L2.
Lihat tutorial [Memindahkan Token ERC-20 dengan Optimism SDK](https://docs.optimism.io/builders/app-developers/tutorials/cross-dom-bridge-erc20) untuk mempelajari cara memindahkan token ERC-20 L1 Anda ke Lisk menggunakan Optimism SDK.
Ingat bahwa langkah withdrawal *tidak akan* berfungsi untuk token yang baru saja Anda buat!
Hal ini sesuai dengan tujuan yang ingin ditunjukkan oleh tutorial ini. */}